apply plugin: 'com.android.application'

def keyStorePropertiesFile = rootProject.file("keystore.properties")
def dev_keyStorePropertiesFile = rootProject.file("devkeystore.properties")
def keyStoreProperties = new Properties()
def dev_keyStoreProperties = new Properties()
keyStoreProperties.load(new FileInputStream(keyStorePropertiesFile))
dev_keyStoreProperties.load(new FileInputStream(dev_keyStorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "ufhealth.integratedmachine.client"
        versionCode  rootProject.ext.versionCode
        versionName  rootProject.ext.versionName
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        flavorDimensions "default"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    productFlavors{
        /**正式版**/
        publish{
            manifestPlaceholders = [APP_NAME:"@string/app_name"]
            buildConfigField 'String', "SERVICE_URL",'"http://60.205.148.14:8000/"'
        }

        /**测试版**/
        develop{
            manifestPlaceholders =[APP_NAME:"@string/app_name_dev"]
            buildConfigField 'String', "SERVICE_URL",'"http://192.168.199.112:8000/"'
        }
    }

    signingConfigs{
        release {
            keyAlias keyStoreProperties['KeyAlias']
            storeFile file(keyStoreProperties['StoreFile'])
            keyPassword keyStoreProperties['KeyPassword']
            storePassword keyStoreProperties['StorePassword']
        }

        debug{
            keyAlias dev_keyStoreProperties['KeyAlias']
            storeFile file(dev_keyStoreProperties['StoreFile'])
            keyPassword dev_keyStoreProperties['KeyPassword']
            storePassword dev_keyStoreProperties['StorePassword']
        }
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled true
            multiDexEnabled true
            zipAlignEnabled true
            shrinkResources true
            /**配置签名密匙*/
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            multiDexEnabled true
            zipAlignEnabled false
            shrinkResources false
            /**不用配置签名密匙*/
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    /**加快dex速度*/
    dexOptions {
        preDexLibraries true
        maxProcessCount 8
        javaMaxHeapSize "2048m"
    }
}

dependencies {
    implementation project(':DevLibrary')
    testImplementation 'junit:junit:4.12'
    implementation 'me.jessyan:autosize:0.9.5'
    implementation 'com.android.support:multidex:1.0.1'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.flyco.dialog:FlycoDialog_Lib:1.3.2@aar'
    implementation 'com.gyf.immersionbar:immersionbar:2.3.2-beta01'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.38'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}